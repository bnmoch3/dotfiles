#!/usr/bin/zsh
# zmodload zsh/zprof

# only run in interactive shells
[[ -o interactive ]] || return

# permissions for new files created
umask 077


# history
HISTFILE=~/.zsh_history
HISTSIZE=10000 # maximum events for internal history
SAVEHIST=10000 # maximum events in history file

setopt HIST_IGNORE_DUPS
setopt HIST_IGNORE_SPACE
setopt APPEND_HISTORY
setopt INC_APPEND_HISTORY
setopt SHARE_HISTORY


# vim mode
bindkey -v
export KEYTIMEOUT=1
zmodload zsh/complist
bindkey -M menuselect 'h' vi-backward-char
bindkey -M menuselect 'k' vi-up-line-or-history
bindkey -M menuselect 'l' vi-forward-char
bindkey -M menuselect 'j' vi-down-line-or-history
autoload -Uz edit-command-line
zle -N edit-command-line
bindkey -M vicmd v edit-command-line

# dir colors
if [[ "$OSTYPE" == "linux-gnu"* ]]; then
  [[ -x /usr/bin/dircolors ]] && eval "$(dircolors -b)"
  alias ls='ls --color=auto'
elif [[ "$OSTYPE" == "darwin"* ]]; then
  [[ -x /usr/local/bin/gdircolors ]] && eval "$(gdircolors -b)"
  alias ls='ls --color=auto'
fi

# aliases
alias ll='ls -alF'
if command -v exa &>/dev/null; then
    alias ll='exa -1l --group-directories-first -h --git'
else
    alias ll='ls -alF'
fi
alias l1='ls -1'

# directory stack
setopt AUTO_PUSHD # push the current directory visited on to the stack.
setopt PUSHD_IGNORE_DUPS # do not store duplirate directories in the stack.
setopt PUSHD_SILENT # do not print the directory stack after using pushd or popd.
# enables quick directory jumping using the directory stack:
# - `alias d='dirs -v'` shows recently visited dirs, numbered.
# - aliases 1â€“9 let you jump to the Nth dir with `cd +N`, e.g. `2` jumps to dir #2.
# - increase `{1..9}` to `{1..100}` to support deeper stacks.
alias d='dirs -v'
for index in {1..9}; do alias "$index"="cd +${index}"; done
unset index


# optimized completion initialization
autoload -Uz compinit
autoload -Uz compinit
if [[ ! -f ~/.zcompdump ]] || [[ $(find ~/.zcompdump -mtime +1 2>/dev/null) ]]; then
  compinit
else
  compinit -C
fi

# auto complete hidden files
comp_options+=(globdots)

# load completion styles after compinit
source "$HOME/dotfiles/zsh/external/completion.zsh"

# auto virtual environment activation
source "$HOME/dotfiles/zsh/auto-venv.zsh"

# functions
source "$HOME/dotfiles/zsh/functions.zsh"

# lazy load nvm
export NVM_DIR="$HOME/.nvm"
nvm() {
  unfunction nvm
  [ -s "$NVM_DIR/nvm.sh" ] && source "$NVM_DIR/nvm.sh"
  [ -s "$NVM_DIR/bash_completion" ] && source "$NVM_DIR/bash_completion"
  nvm "$@"
}

# deno (lazy load)
if [[ -d "$HOME/.deno" ]]; then
    deno() {
        unfunction deno # first time deno is called remove this function
        . "/home/bnm/.deno/env" # load the real deno environment
        deno "$@" # call the real deno command with all the args
    }
fi

# fzf (lazy load)
fzf() {
  [[ -f "$HOME/.fzf.zsh" ]] && source "$HOME/.fzf.zsh"
  unfunction fzf
  fzf "$@"
}



eval "$(atuin init zsh --disable-up-arrow)"
eval "$(starship init zsh)"

# zprof
